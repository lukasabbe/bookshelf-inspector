plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
}
repositories {
    maven { url "https://maven.shedaniel.me/" }
    mavenCentral()
}
neoForge {
    version = neoforge_version
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    dependencies {
        api "me.shedaniel.cloth:cloth-config-neoforge:${cloth_config}"
    }

    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric
        }
        client {
            client()
        }
        data {
            clientData()
        }
        server {
            server()
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

dependencies {
    jarJar(implementation("org.yaml:snakeyaml:2.4")){}
    additionalRuntimeClasspath "org.yaml:snakeyaml:2.4"
}

sourceSets.main.resources { srcDir 'src/generated/resources' }