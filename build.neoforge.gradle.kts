plugins {
    id("net.neoforged.moddev") version "2.0.140"
    id("neoforge-mutex")
    id("me.modmuss50.mod-publish-plugin") version "2.0.1"
}

version = "${property("mod.version")}+${sc.current.version}"
base.archivesName = "${property("mod.id") as String}-neoforge"

val requiredJava = when {
    sc.current.parsed >= "26.1" -> JavaVersion.VERSION_25
    sc.current.parsed >= "1.20.5" -> JavaVersion.VERSION_21
    sc.current.parsed >= "1.18" -> JavaVersion.VERSION_17
    sc.current.parsed >= "1.17" -> JavaVersion.VERSION_16
    else -> JavaVersion.VERSION_1_8
}

repositories {
    /**
     * Restricts dependency search of the given [groups] to the [maven URL][url],
     * improving the setup speed.
     */
    fun strictMaven(url: String, alias: String, vararg groups: String) = exclusiveContent {
        forRepository { maven(url) { name = alias } }
        filter { groups.forEach(::includeGroup) }
    }
    strictMaven("https://www.cursemaven.com", "CurseForge", "curse.maven")
    strictMaven("https://api.modrinth.com/maven", "Modrinth", "maven.modrinth")
    maven("https://maven.shedaniel.me/")
}


dependencies {
    val yamlVersion = property("deps.yaml").toString()
    val yamlCoords = "org.yaml:snakeyaml"

    jarJar(yamlCoords) {
        this as ExternalModuleDependency
        version {
            prefer(yamlVersion)
        }
    }

    implementation("$yamlCoords:$yamlVersion")

}

neoForge {
    version = property("deps.neo_loader") as String

    mods {
        register(property("mod.id") as String) {
            sourceSet(sourceSets.main.get())
        }
    }

    runs {
        register("client") {
            gameDirectory = file("../../run/")
            client()
        }

        register("server") {
            gameDirectory = file("../../run/")
            server()
        }
    }


    dependencies {
        //Cloth config
        val clothConfig = property("deps.cloth_config") as String
        api ("me.shedaniel.cloth:cloth-config-neoforge:$clothConfig")
    }
}

java {
    withSourcesJar()
    targetCompatibility = requiredJava
    sourceCompatibility = requiredJava
}

tasks {
    processResources {
        fun MutableMap<String, String>.register(key: String, property: String) {
            val value: String = sc.properties[property]
            inputs.property(key, value)
            set(key, value)
        }

        val props = buildMap {
            register("id", "mod.id")
            register("name", "mod.name")
            register("version", "mod.version")
            register("minecraft", "mod.mc_compat")
        }

        filesMatching("META-INF/neoforge.mods.toml") { expand(props) }

        val mixinJava = "JAVA_${requiredJava.majorVersion}"
        filesMatching("*.mixins.json") { expand("java" to mixinJava) }

        exclude("fabric.mod.json", "*.ct", "*.classtweaker")
    }

    named("createMinecraftArtifacts") {
        dependsOn("stonecutterGenerate")
    }

    register<Copy>("buildAndCollect") {
        group = "build"
        description = "Builds mod jars and copies results to `build/libs/{mod version}/`"

        inputs.property("version", project.property("mod.version"))
        from(jar.flatMap { it.archiveFile }, named<Jar>("sourcesJar").flatMap { it.archiveFile })
        into(rootProject.layout.buildDirectory.file("libs/${project.property("mod.version")}"))
    }
}
publishMods {
    val mrToken = providers.environmentVariable("MODRINTH_TOKEN")
    val modVersion = "${project.property("mod.version")}+${stonecutter.current.version}"
    type.set(BETA)
    file.set( tasks.jar.map { it.archiveFile.get() })
    changelog.set(provider { rootProject.file("CHANGELOG.md").readText() })
    modLoaders.add("neoforge")
    version.set(modVersion)
    displayName.set(modVersion)

    modrinth {
        projectId = "rOrXjyPb"
        accessToken = mrToken
        minecraftVersions.add(stonecutter.current.version)
    }
}